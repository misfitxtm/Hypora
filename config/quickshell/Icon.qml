import QtQuick

// Line icon drawn from inline SVG and colored from the theme. No icon font needed.
// Shapes are from Feather (MIT, feathericons.com) unless noted.
// `level` (0..1) drives the wifi, volume and battery variants.
Image {
    id: root
    property string name
    property color color: Theme.fg
    property real size: 16
    property real level: 1
    property bool charging: false

    width: size
    height: size
    sourceSize: Qt.size(size, size)
    smooth: true

    function hex(c) {
        const h = v => Math.round(v * 255).toString(16).padStart(2, "0")
        return "#" + h(c.r) + h(c.g) + h(c.b)
    }

    function body(name, level, charging, c) {
        const speaker = '<polygon points="11 5 6 9 2 9 2 15 6 15 11 19 11 5"/>'
        const dim = on => on ? '' : ' opacity="0.3"'
        switch (name) {
        case "wifi":
            return `<path d="M1.42 9a16 16 0 0 1 21.16 0"${dim(level > 0.75)}/>`
                 + `<path d="M5 12.55a11 11 0 0 1 14.08 0"${dim(level > 0.5)}/>`
                 + `<path d="M8.53 16.11a6 6 0 0 1 6.95 0"${dim(level > 0.25)}/>`
                 + '<line x1="12" y1="20" x2="12.01" y2="20"/>'
        case "wifi-off":
            return '<line x1="1" y1="1" x2="23" y2="23"/><path d="M16.72 11.06A10.94 10.94 0 0 1 19 12.55"/>'
                 + '<path d="M5 12.55a10.94 10.94 0 0 1 5.17-2.39"/><path d="M10.71 5.05A16 16 0 0 1 22.58 9"/>'
                 + '<path d="M1.42 9a15.91 15.91 0 0 1 4.7-2.88"/><path d="M8.53 16.11a6 6 0 0 1 6.95 0"/>'
                 + '<line x1="12" y1="20" x2="12.01" y2="20"/>'
        case "ethernet":   // after Lucide "ethernet-port" (ISC)
            return '<path d="M15 20l3-3h2a2 2 0 0 0 2-2V6a2 2 0 0 0-2-2H4a2 2 0 0 0-2 2v9a2 2 0 0 0 2 2h2l3 3z"/>'
                 + '<path d="M6 8v1M10 8v1M14 8v1M18 8v1"/>'
        case "volume":
            if (level <= 0) return speaker + '<line x1="23" y1="9" x2="17" y2="15"/><line x1="17" y1="9" x2="23" y2="15"/>'
            return speaker + '<path d="M15.54 8.46a5 5 0 0 1 0 7.07"/>'
                 + `<path d="M19.07 4.93a10 10 0 0 1 0 14.14"${dim(level > 0.5)}/>`
        case "battery":
            return '<rect x="1" y="6" width="18" height="12" rx="2" ry="2"/><line x1="23" y1="13" x2="23" y2="11"/>'
                 + (charging ? '<polyline points="11 8.5 8 12 12 12 9 15.5" stroke-width="1.6"/>'
                             : `<rect x="3.5" y="8.5" width="${Math.max(0.5, 13 * level)}" height="7" rx="0.5" fill="${c}" stroke="none"/>`)
        case "bluetooth":
            return '<polyline points="6.5 6.5 17.5 17.5 12 23 12 1 17.5 6.5 6.5 17.5"/>'
        case "bell":
            return '<path d="M18 8A6 6 0 0 0 6 8c0 7-3 9-3 9h18s-3-2-3-9"/><path d="M13.73 21a2 2 0 0 1-3.46 0"/>'
        case "bell-off":
            return '<path d="M13.73 21a2 2 0 0 1-3.46 0"/><path d="M18.63 13A17.89 17.89 0 0 1 18 8"/>'
                 + '<path d="M6.26 6.26A5.86 5.86 0 0 0 6 8c0 7-3 9-3 9h14"/><path d="M18 8a6 6 0 0 0-9.33-5"/>'
                 + '<line x1="1" y1="1" x2="23" y2="23"/>'
        case "sun":
            return '<circle cx="12" cy="12" r="5"/><line x1="12" y1="1" x2="12" y2="3"/><line x1="12" y1="21" x2="12" y2="23"/>'
                 + '<line x1="4.22" y1="4.22" x2="5.64" y2="5.64"/><line x1="18.36" y1="18.36" x2="19.78" y2="19.78"/>'
                 + '<line x1="1" y1="12" x2="3" y2="12"/><line x1="21" y1="12" x2="23" y2="12"/>'
                 + '<line x1="4.22" y1="19.78" x2="5.64" y2="18.36"/><line x1="18.36" y1="5.64" x2="19.78" y2="4.22"/>'
        case "moon":
            return '<path d="M21 12.79A9 9 0 1 1 11.21 3 7 7 0 0 0 21 12.79z"/>'
        case "lock":
            return '<rect x="3" y="11" width="18" height="11" rx="2" ry="2"/><path d="M7 11V7a5 5 0 0 1 10 0v4"/>'
        case "logout":
            return '<path d="M9 21H5a2 2 0 0 1-2-2V5a2 2 0 0 1 2-2h4"/><polyline points="16 17 21 12 16 7"/>'
                 + '<line x1="21" y1="12" x2="9" y2="12"/>'
        case "reboot":
            return '<polyline points="23 4 23 10 17 10"/><path d="M20.49 15a9 9 0 1 1-2.12-9.36L23 10"/>'
        case "power":
            return '<path d="M18.36 6.64a9 9 0 1 1-12.73 0"/><line x1="12" y1="2" x2="12" y2="12"/>'
        case "chevron":
            return '<polyline points="9 18 15 12 9 6"/>'
        case "chevron-left":
            return '<polyline points="15 18 9 12 15 6"/>'
        case "camera":
            return '<path d="M23 19a2 2 0 0 1-2 2H3a2 2 0 0 1-2-2V8a2 2 0 0 1 2-2h4l2-3h6l2 3h4a2 2 0 0 1 2 2z"/>'
                 + '<circle cx="12" cy="13" r="4"/>'
        case "film":
            return '<rect x="2" y="2" width="20" height="20" rx="2.18" ry="2.18"/><line x1="7" y1="2" x2="7" y2="22"/>'
                 + '<line x1="17" y1="2" x2="17" y2="22"/><line x1="2" y1="12" x2="22" y2="12"/>'
                 + '<line x1="2" y1="7" x2="7" y2="7"/><line x1="2" y1="17" x2="7" y2="17"/>'
                 + '<line x1="17" y1="17" x2="22" y2="17"/><line x1="17" y1="7" x2="22" y2="7"/>'
        case "music":
            return '<path d="M9 18V5l12-2v13"/><circle cx="6" cy="18" r="3"/><circle cx="18" cy="16" r="3"/>'
        case "message":
            return '<path d="M21 15a2 2 0 0 1-2 2H7l-4 4V5a2 2 0 0 1 2-2h14a2 2 0 0 1 2 2z"/>'
        case "tool":
            return '<path d="M14.7 6.3a1 1 0 0 0 0 1.4l1.6 1.6a1 1 0 0 0 1.4 0l3.77-3.77a6 6 0 0 1-7.94 7.94l-6.91 6.91'
                 + 'a2.12 2.12 0 0 1-3-3l6.91-6.91a6 6 0 0 1 7.94-7.94l-3.76 3.76z"/>'
        case "calendar":
            return '<rect x="3" y="4" width="18" height="18" rx="2" ry="2"/><line x1="16" y1="2" x2="16" y2="6"/>'
                 + '<line x1="8" y1="2" x2="8" y2="6"/><line x1="3" y1="10" x2="21" y2="10"/>'
        case "cpu":
            return '<rect x="7" y="7" width="10" height="10" rx="1"/><rect x="3.5" y="3.5" width="17" height="17" rx="2"/>'
                 + '<line x1="9" y1="1.5" x2="9" y2="3.5"/><line x1="15" y1="1.5" x2="15" y2="3.5"/>'
                 + '<line x1="9" y1="20.5" x2="9" y2="22.5"/><line x1="15" y1="20.5" x2="15" y2="22.5"/>'
                 + '<line x1="1.5" y1="9" x2="3.5" y2="9"/><line x1="1.5" y1="15" x2="3.5" y2="15"/>'
                 + '<line x1="20.5" y1="9" x2="22.5" y2="9"/><line x1="20.5" y1="15" x2="22.5" y2="15"/>'
        case "memory":
            return '<rect x="2" y="6.5" width="20" height="11" rx="1.5"/>'
                 + '<line x1="6" y1="17.5" x2="6" y2="21"/><line x1="12" y1="17.5" x2="12" y2="21"/>'
                 + '<line x1="18" y1="17.5" x2="18" y2="21"/>'
                 + '<line x1="6" y1="10" x2="6" y2="14"/><line x1="12" y1="10" x2="12" y2="14"/>'
                 + '<line x1="18" y1="10" x2="18" y2="14"/>'
        case "thermometer":
            return '<path d="M14 14.76V3.5a2.5 2.5 0 0 0-5 0v11.26a4.5 4.5 0 1 0 5 0z"/>'
        case "gpu":
            return '<rect x="1.5" y="5.5" width="21" height="13" rx="2"/><circle cx="8" cy="12" r="3"/>'
                 + '<line x1="14" y1="9.5" x2="19" y2="9.5"/><line x1="14" y1="12" x2="19" y2="12"/>'
                 + '<line x1="14" y1="14.5" x2="19" y2="14.5"/>'
        case "home":
            return '<path d="M3 9l9-7 9 7v11a2 2 0 0 1-2 2H5a2 2 0 0 1-2-2z"/><polyline points="9 22 9 12 15 12 15 22"/>'
        case "folder":
            return '<path d="M22 19a2 2 0 0 1-2 2H4a2 2 0 0 1-2-2V5a2 2 0 0 1 2-2h5l2 3h9a2 2 0 0 1 2 2z"/>'
        case "download":
            return '<path d="M21 15v4a2 2 0 0 1-2 2H5a2 2 0 0 1-2-2v-4"/><polyline points="7 10 12 15 17 10"/>'
                 + '<line x1="12" y1="15" x2="12" y2="3"/>'
        case "image":
            return '<rect x="3" y="3" width="18" height="18" rx="2" ry="2"/><circle cx="8.5" cy="8.5" r="1.5"/>'
                 + '<polyline points="21 15 16 10 5 21"/>'
        case "monitor":
            return '<rect x="2" y="3" width="20" height="14" rx="2" ry="2"/><line x1="8" y1="21" x2="16" y2="21"/>'
                 + '<line x1="12" y1="17" x2="12" y2="21"/>'
        case "terminal":
            return '<polyline points="4 17 10 11 4 5"/><line x1="12" y1="19" x2="20" y2="19"/>'
        case "search":
            return '<circle cx="11" cy="11" r="8"/><line x1="21" y1="21" x2="16.65" y2="16.65"/>'
        case "zap":
            return '<polygon points="13 2 3 14 12 14 11 22 21 10 12 10 13 2"/>'
        case "sliders":
            return '<line x1="4" y1="21" x2="4" y2="14"/><line x1="4" y1="10" x2="4" y2="3"/>'
                 + '<line x1="12" y1="21" x2="12" y2="12"/><line x1="12" y1="8" x2="12" y2="3"/>'
                 + '<line x1="20" y1="21" x2="20" y2="16"/><line x1="20" y1="12" x2="20" y2="3"/>'
                 + '<line x1="1" y1="14" x2="7" y2="14"/><line x1="9" y1="8" x2="15" y2="8"/><line x1="17" y1="16" x2="23" y2="16"/>'
        case "grid":
            return '<rect x="3" y="3" width="7" height="7"/><rect x="14" y="3" width="7" height="7"/>'
                 + '<rect x="14" y="14" width="7" height="7"/><rect x="3" y="14" width="7" height="7"/>'
        default:
            return '<circle cx="12" cy="12" r="9"/>'
        }
    }

    source: {
        const c = hex(color)
        const svg = '<svg xmlns="http://www.w3.org/2000/svg" viewBox="0 0 24 24" fill="none" '
                  + `stroke="${c}" stroke-width="2" stroke-linecap="round" stroke-linejoin="round">`
                  + body(name, level, charging, c) + '</svg>'
        return "data:image/svg+xml;utf8," + encodeURIComponent(svg)
    }
}
